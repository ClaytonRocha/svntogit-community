# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.2.2
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-loguru python-pytest-runner)
optdepends=('python-loguru: for logging support')
source=(https://pypi.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('cd3a9efbd71e33a343de4793c1b415129fbe280ea6b6bc40e84fc1ada7d409139d8bef6c7e2954ef6cd3b9407d817b07a61093d1a8b9de70f6af0b6aba3ebc8d'
            'SKIP')
b2sums=('ae36d918bfd125ed1c01a71f8b448390b70071eca8c404f2ce89312c0de8296b29026cd72baa1eeba940d84e2f0b5275ea0ee96735fdecd1b7fb8f5d42ac1b57'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  export PYTHONPATH="$PWD/build:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
