# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-parser
pkgver=1.64
pkgrel=2
pkgdesc="Modules for parsing/translating POD format documents"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Parser"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MA/MAREKR/Pod-Parser-$pkgver.tar.gz")
sha512sums=('9d5bf549fa7bd4dfbb615209ab2e43e5efc531abf86a468036d27b5b1409aa30333adf77b7b7ccfb48bc626ae503ba85e2cd8a6432554d7ecb22844d39e18572')

build() {
  cd Pod-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Parser-$pkgver
  make test
}

package() {
  cd Pod-Parser-$pkgver
  make DESTDIR="$pkgdir" install
}
