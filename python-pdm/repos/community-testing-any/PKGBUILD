# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pdm
pkgname=python-pdm
# WARNING: vendored libs may change in patch level versions...
pkgver=1.15.4
pkgrel=1
pkgdesc="A modern Python package manager with PEP 582 support"
arch=(any)
url="https://github.com/pdm-project/pdm"
license=(MIT)
depends=(
  python-blinker
  python-click
  python-dotenv
  python-findpython
  python-installer
  python-packaging
  python-pdm-pep517
  python-pep517
  python-pip
  python-platformdirs
  python-resolvelib
  python-shellingham
  python-tomli
  python-tomlkit
  python-wheel
)
# packages devendored from pdm sources
depends+=(
  python-colorama
  python-halo
  python-log_symbols
)
makedepends=(python-build)
checkdepends=(python-pytest python-pytest-mock python-pytest-xdist)
optdepends=('python-keyring: for storing credentials')
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-1.15.4-devendor.patch
)
sha512sums=('35e32d6ccb2e4aa874f413e3d5f3a92c772a7ee6d7dd3f75a40ae082148cfb389585a1371e130ebdbc75ec25180d5f525177012198342e3ece354bfc799735e1'
            '541d36828f2a11a98ba30ba521be505da7d7552097f1bedf381a4b5e9f637f7cdaf127bad0efc5851b69b5bc4abe4d569165444c27d9490df1155814123b23ea')
b2sums=('e2c2e3437ee290aa849b1e04e51932fe58af1c77581a37a53b2885db6fc622c819688e222b2ef889192a62c2552a3f180fa3924f9c688f72ad79c5ee16180d03'
        '929b3dc488a2479efd86dd950bc39429a363926eabbd0ad0a298d42f6bc38d165f0db62ede8bcdf6eba6d881bb4e1218f5bbca9dad779afa73fa8e883ca6949f')

prepare() {
  # devendor bundled libraries
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-1.15.4-devendor.patch
  # remove vendor dir as we use system dependencies
  rm -frv $_name-$pkgver/pdm/_vendor
}

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null || echo "Failing tests because reasons: https://github.com/pdm-project/pdm/issues/1175"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
