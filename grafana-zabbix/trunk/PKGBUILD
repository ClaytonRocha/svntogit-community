# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=4.2.6
pkgrel=2
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn libfaketime go git nodejs-lts-gallium)
source=("$pkgname-$pkgver-retagged-1.tar.gz::https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz"
        grafana-zabbix-sass-update-for-node-16.patch::https://github.com/alexanderzobnin/grafana-zabbix/commit/a09d8a983e051cbe4a959c40224426edc3468d10.patch)
sha256sums=('913efd1d6bfb903eb7e40d79d58fc1d9cf5041d4764bc1f34f1c5997439de1ab'
	        'ec1c6863ba97db0da505569b0992bc6a3158d54621db20235635e4fbd36eb15c')

prepare() {
	cd "$pkgname-$pkgver"
	patch -Np1 -i ../grafana-zabbix-sass-update-for-node-16.patch
}

build() {
	cd "$pkgname-$pkgver"
	make install
	make build
	make dist
}

check() {
	cd "$pkgname-$pkgver"
	# Force UTC timezone so that tests pass, even after a DST change
	PATH="$PATH:/build/go/bin" TZ=UTC make test lint
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r dist/* "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
