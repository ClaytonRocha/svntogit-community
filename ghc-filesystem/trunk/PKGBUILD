# Maintainer: David Runge <dvzrv@archlinux.org>

_name=filesystem
pkgname=ghc-filesystem
pkgver=1.5.12
pkgrel=1
pkgdesc="An implementation of C++17 std::filesystem for C++11 /C++14/C++17/C++20"
arch=(any)
url="https://github.com/gulrak/filesystem"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::https://github.com/gulrak/filesystem/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('2cba74921104fa84547288ff983260ce1e81967df6a7d2a334074826c355c72945ad64e6979cd302a23c5e3a398990706b01fc573c046512e9f508edca9da12c')
b2sums=('9eb0bce08659396f3ef254b0ad772ae9ad29654aef3c90ab1f9eee1c4f830544ad947680e072fcf7d3c251a3afb77447617610c49931d50743444a6b88b4745d')

build() {
  cmake \
    -B build \
    -S $_name-$pkgver \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --output-on-failure --test-dir build/test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
