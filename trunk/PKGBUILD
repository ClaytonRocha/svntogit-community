# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Alexander Phinikarides <alexisph _AT_ gmail _DOT_ com>

pkgbase='python-pyroute2'
_pkgbase="${pkgbase//python-/}"
pkgname=("python-${_pkgbase}")
pkgdesc="A pure Python netlink and Linux network configuration library"
pkgver=0.6.11
pkgrel=1
arch=('any')
url="https://docs.pyroute2.org/"
license=('GPL2' 'Apache')
depends=('python')
makedepends=('python' 'python-pip' 'python-setuptools' 'python-setuptools-scm' 'python-sphinx' 'git' 'python-aafigure' 'twine' 'python-black')
checkdepends=('python-pytest')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/svinota/${_pkgbase}/archive/${pkgver}.tar.gz"
        'fix_makefile.patch')
sha512sums=('cc7a46d9738d048dc9008780e03ca24710f3ea395572057b17252fc5d0b71ab47cf3c053b249ee3d8626c1589d98e17e6513fd2d2ec266ef5f8ca31e2017bd51'
            '1a8b2a9533d5afc8af77ee112a42134b91f3d575d7db3774b02e10c6f66133907b52485b0dd9d82bf983046a713f574dcf54445945f8e58b2c387b2e547895e0')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # Update the version manually. Otherwise it would look for a git tag which is not present in the distribution.
  sed -i "s/version = get_project_version.*$/version = '${pkgver}'/" util/update_version.py
  sed -i "s/^VERSION=.*\$/VERSION='${pkgver}'/" tests/conf.sh

  # there is some weirdness in the Makefile with respect to depencies
  # TODO : this needs to be submitted upstream
  patch -p1 -N -i "${srcdir}/fix_makefile.patch"

}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make dist
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # upstream tests are not meant to be executed downstream
  :
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make root="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
