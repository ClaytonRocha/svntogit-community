# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.44.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=(x86_64)
url="https://guitarix.org"
license=(GPL3)
groups=(ladspa-plugins lv2-plugins pro-audio)
depends=(atkmm bluez-libs cairo cairomm gcc-libs glibc glibmm gtkmm3 libsigc++
libx11 pangomm ttf-roboto)
makedepends=(avahi boost curl gdk-pixbuf2 eigen faust fftw glib2 gperf gtk3
intltool jack ladspa liblo liblrdf lilv lv2 pango sassc waf zita-convolver
zita-resampler)
optdepends=('new-session-manager: for session management')
provides=(guitarix2 libgxw.so libgxwmm.so ladspa-host lv2-host)
conflicts=(guitarix2)
replaces=(guitarix2)
options=(debug)
source=($pkgname-$pkgver.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-$pkgver.tar.xz)
sha512sums=('30520600ef60379770f40901a19219a044d442c77d0808df72957eb5f7fba7e6aef7ec2217735683033697144c3c9850963e89941d31ebb6b7490507a3e35f75')
b2sums=('1abc470ec7adc3286ee3edefc7652e95ec20d73c104cd20d9521c85d68ff4e91b70465ff05b7acc2c10b1902c976c6c93f171325494126f75707d5e3ea9affb8')

build() {
  cd $pkgname-$pkgver
  export LINKFLAGS="$LDFLAGS"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --cxxflags='-flto' \
                --ldflags="$LDFLAGS"
  waf build -vv -j1
}

package() {
  depends+=(libavahi-common.so libavahi-gobject.so libboost_iostreams.so
  libcurl.so libfftw3f.so libgdk-3.so libgdk_pixbuf-2.0.so libgio-2.0.so
  libglib-2.0.so libgobject-2.0.so libjack.so liblilv-0.so liblo.so liblrdf.so
  libpangocairo-1.0.so libpango-1.0.so libsndfile.so libzita-convolver.so
  libzita-resampler.so)

  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"
  # docs
  install -vDm 644 {changelog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
