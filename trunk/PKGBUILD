# Maintainer: Alad Wenter <alad@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.9.6
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('x86_64')
url='https://pcsclite.apdu.fr/'
license=('BSD')
depends=('python' 'systemd' 'libsystemd.so' 'libudev.so')
makedepends=('pkg-config')
provides=('libpcsclite.so' 'libpcscspy.so')
options=('!docs')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <rousseau@debian.org>
source=("https://pcsclite.apdu.fr/files/pcsc-lite-${pkgver}.tar.bz2"{,.asc}
        https://github.com/LudovicRousseau/PCSC/commit/76c99cdd0ad5f6d57c4f8bde25dd2918bb8af3f7.patch)
sha256sums=('fdb3fe8f68049019523801909b2a336d0d4ee315edafe363c30d375e3706537a'
            'SKIP'
            '04059db5f72187f831a2af3dc7b818f375835398a003a559fe08844a1c5f4778')

prepare() {
  cd "pcsc-lite-$pkgver"
  
  # Fix compile issue "'dwClientID' may be used uninitialized"
  patch -Np1 < ../76c99cdd0ad5f6d57c4f8bde25dd2918bb8af3f7.patch
}


build() {
  cd "pcsc-lite-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-filter \
    --enable-ipcdir=/run/pcscd \
    --enable-libudev \
    --enable-usbdropdir=/usr/lib/pcsc/drivers \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "pcsc-lite-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m0644 "$srcdir/pcsc-lite-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -d "$pkgdir/usr/lib/pcsc/drivers"
}
