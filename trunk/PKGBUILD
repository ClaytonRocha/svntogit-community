# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libspecbleach
pkgver=0.1.5
pkgrel=1
pkgdesc="C library for audio noise reduction"
arch=(x86_64)
url="https://github.com/lucianodato/libspecbleach"
license=(LGPL2.1)
depends=(glibc)
makedepends=(fftw meson)
provides=(libspecbleach.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/lucianodato/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('32340880d788a2888b1bb7a95bfcd50fdf416d0652b50838398cb20d3cbd3e6970cd9fe6ca636ba67d2a16dc345dadda16ef064c95e6003a4bc6c3f25eeed506')
b2sums=('17e45a85ac5b41bef40e46bc7b128584968a81b84d2058c68a66c84a9affe2614fa2d63da59db31f325fbd25d86815bf7c3f91f574e56b7a0307e3710d106149')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  depends+=(libfftw3f.so)

  meson install -C build --destdir="$pkgdir"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/example/* -t "$pkgdir/usr/share/doc/$pkgname/example/"
}
