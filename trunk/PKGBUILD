# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=noise-repellent
pkgver=0.2.2
pkgrel=1
pkgdesc="An lv2 plug-in for broadband noise reduction"
arch=(x86_64)
url="https://github.com/lucianodato/noise-repellent"
license=(LGPL3)
groups=(lv2-plugins pro-audio)
depends=(fftw glibc libspecbleach lv2-host)
makedepends=(lv2 meson)
checkdepends=(lv2lint)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/lucianodato/$pkgname/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-0.2.2-hide_symbols.patch::https://github.com/lucianodato/noise-repellent/pull/110/commits/1d3f2a0c12dbd104ea7e77d3fdbb33c720e44fa2.patch
)
sha512sums=('68d5e675a5d402c98b4125e2ac48f0e558ffaed2a8036c600aaaec7adcba38c6a0998b489352b0570b7db1fb23e25e74b402ef46ada77fd615f2c5d60821d998'
            '13022de4f252d9763ad6e456b1d0e030da78cc67313cc1a9bb42dd836ed339d0681972251c25e8f6ffbc74f67fb0fd655c7baf34df3f873a187b65ca2d94681d')
b2sums=('3449d38c77f2206de3d9036ce4afd743b9f55f32f34caba82debe9114226f3163e27adee8fec9832890e4d7d1fb473c3ded3c1892b442f5ad97ea08866039d35'
        'add996dcfc664795d79f8c4ec35338f4500e304fb0736419484993891bd3b88757182a0167d5dda239fb9baf442c3cf5f51a1184b135e02797e665988cc11685')

prepare() {
  # hide plugin symbols: https://github.com/lucianodato/noise-repellent/issues/109
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.2-hide_symbols.patch
}

build() {
  arch-meson build $pkgname-$pkgver
  meson compile -C build
}

check() {
  local _links=(
    "https://github.com/lucianodato/noise-repellent#new"
    "https://github.com/lucianodato/noise-repellent-stereo#new"
  )
  for _link in "${_links[@]}"; do
    lv2lint -Mpack -I build/ "${_link}"
  done
}

package() {
  depends+=(libspecbleach.so)

  meson install -C build --destdir="$pkgdir"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
