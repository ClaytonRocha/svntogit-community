# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Nikola
pkgname=nikola
pkgver=8.2.1
pkgrel=2
pkgdesc="A static site and blog generator"
arch=(any)
url="https://getnikola.com/"
license=(MIT)
# still requires python-setuptools due to the use of pkg_resources
depends=(python-babel python-blinker python-doit python-dateutil
python-docutils python-logbook python-lxml python-mako python-markdown
python-natsort python-piexif python-pillow python-pygments python-pyrss2gen
python-requests python-setuptools python-unidecode python-yapsy)
makedepends=(jupyter-notebook python-aiohttp python-build python-ghp-import
python-husl python-installer python-ipykernel python-jinja python-micawber
python-phpserialize python-pygal python-pyphen python-ruamel-yaml python-toml
python-typogrify python-watchdog python-wheel zeromq)
checkdepends=(python-freezegun python-pytest)
optdepends=(
  'jupyter-notebook: for .ipynb support'
  'python-aiohttp: for `nikola auto`'
  'python-ghp-import: for upload to Github pages'
  'python-hsluv: for section color mixing'
  'python-html5lib: for HTML5 minify and format support'
  'python-husl: for optional color support'
  'python-ipykernel: for .ipynb support'
  'python-jinja: for jinja2 based themes'
  'python-micawber: for embedding media in posts'
  'python-phpserialize: for Wordpress import'
  'python-pygal: for SVG graph plotting'
  'python-pyphen: for hyphenation support'
  'python-ruamel-yaml: for YAML support'
  'python-toml: for TOML support'
  'python-typogrify: for typographical enhancements'
  'python-watchdog: for `nikola auto`'
)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc}
  $pkgname-8.2.1-pygments_2.12.0.patch::https://github.com/getnikola/nikola/commit/097fe282bb95b898d329a945c0bb7098a3ac7a9c.patch
)
sha512sums=('5893cc7a3eb876fc1c165e91bcc03db9674c9b6569b89fb10ce111bc9f3a7f8b72d54641429a8d882a3a2ac9aa59e5ed665b23f8d37c9679cc9aff5b8d839ccb'
            'SKIP'
            '9f3388a715b354545f6122da37b87d92bb5a72937d582a7394e3dc9727b3f2bcfb31adb2ea617140c06c794a8421f1499cd23cba35a60dd9a8f2498cf2cd0fb4')
b2sums=('4e9514984d5e813421f374d67018d266aa56c63674b8c1070ad2bc0685d6d59a08be38b1422808564653d6c4f80e2ad40ca7facb7d0ea0185bcd13f136c4b915'
        'SKIP'
        'f2763602a0a19cd1b45be87fbf9fd14c7d99542154ffa1ceadb8a9b896a6c55b4dc416be6c48ee65d09dc8f2817742f2b3747d12d77ecdceb277729337f39f88')
validpgpkeys=('DF32C99CFF84282CF9D4CF7171023DBD5EAAEA16') # Chris Warrick <kwpolska@gmail.com>

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-8.2.1-pygments_2.12.0.patch
  sed -e '/addopts/d' -i $_name-$pkgver/setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation

  # shell completions
  python -m $pkgname tabcompletion --shell bash --hardcode-tasks > ${pkgname}_bash
  python -m $pkgname tabcompletion --shell zsh --hardcode-tasks > ${pkgname}_zsh
  # ensure that the shell completion is assigned to the correct executable
  sed -e "s/__main__.py/$pkgname/g" -i "${pkgname}_"{bash,zsh}
}

check() {
  cd $_name-$pkgver
  pytest -vv -k "not test_compiling_markdown"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ${pkgname}_bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 ${pkgname}_zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
