# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.9.0
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library with parallel support (${_mpi} version)"
arch=(x86_64)
url="https://www.unidata.ucar.edu/software/netcdf/"
license=(custom)
depends=("hdf5-${_mpi}" "pnetcdf-${_mpi}" curl libxml2)
makedepends=(cmake)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
checkdepends=(inetutils unzip)
provides=("${_pkg}")
conflicts=("${_pkg}")
options=(!makeflags)
source=(https://github.com/Unidata/netcdf-c/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        netcdf-4.9.0-fix-cmake-typo.patch::https://github.com/Unidata/netcdf-c/commit/80a6611cfca98a5979c5ac69cba56a49af8c1e48.patch)
sha256sums=('9f4cb864f3ab54adb75409984c6202323d2fc66c003e5308f3cdf224ed41c0a6'
            'ed45ae6c49cf8dcddaadef4c5cf403049bf3f761187413d7b03754d319345d6a')

prepare() {
  patch -p1 -d ${_pkg}-c-${pkgver} < netcdf-4.9.0-fix-cmake-typo.patch
  # https://github.com/Unidata/netcdf-c/issues/2242
  sed -i "/add_sh_test(ncdap tst_remote3)/d" ${_pkg}-c-${pkgver}/ncdap_test/CMakeLists.txt
}

build() {
  export CC=mpicc
  # Disabling remote tests because of https://github.com/Unidata/netcdf-c/issues/2188
  cmake -B build -S ${_pkg}-c-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_BYTERANGE=ON \
    -DENABLE_CDF5=ON \
    -DENABLE_PNETCDF=ON \
    -DENABLE_DAP_LONG_TESTS=OFF \
    -DENABLE_DAP_REMOTE_TESTS=OFF \
    -DENABLE_EXAMPLE_TESTS=ON \
    -DENABLE_EXTRA_TESTS=ON \
    -DENABLE_FILTER_TESTING=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DENABLE_UNIT_TESTS=ON
  make -C build
}


check() {
  # This is required starting with OpenMPI 3.0 when trying to run more
  # processes than the number of available cores
  export OMPI_MCA_rmaps_base_oversubscribe=yes

  # https://github.com/Unidata/netcdf-c/issues/2418
  make -C build test || echo "Tests failed"
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_pkg}-c-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
}
