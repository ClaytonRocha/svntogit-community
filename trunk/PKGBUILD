# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=mitmproxy2swagger
pkgver=0.5.1
pkgrel=1
pkgdesc='Automagically reverse-engineer REST APIs via capturing traffic'
url='https://github.com/alufers/mitmproxy2swagger'
arch=('any')
license=('MIT')
depends=(python mitmproxy python-ruamel-yaml python-json-stream)
makedepends=(python-installer python-poetry)
source=(https://github.com/alufers/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/alufers/mitmproxy2swagger/commit/bad11f1043c644232de7fe050839a2c0aa48aea6.patch)
b2sums=('67ab1f879769aa20fc6c33e5ff94d18810b80200c5e1a5fab0261f3b7b66e5bb934d22fd8abd9db69510797aaa180697abcfa62aac6a90b517ba3786aa70191c'
        '94f787e36abab970226c93ec7d37f548b6db37f521b54d426eb00b80e60e67d6e06fb07f705d9967fddd32bb48f80c44fac279541792f44d72982832efe3dc7e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../bad11f1043c644232de7fe050839a2c0aa48aea6.patch
}

build() {
  cd ${pkgname}-${pkgver}
  poetry build --format wheel
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
