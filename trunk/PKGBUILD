# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-traitlets
pkgver=5.2.2
pkgrel=1
pkgdesc="A configuration system for Python applications"
arch=('any')
url="https://traitlets.readthedocs.io/en/stable/"
license=('BSD')
depends=('python-decorator')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/t/traitlets/traitlets-$pkgver.tar.gz")
md5sums=('6a28a5b81bf4ad369bfec50d539e8a51')

prepare() {
  cd traitlets-$pkgver
  sed -e 's|(5, 2, 2, \"final\")|(5, 2, 2)|' -i traitlets/_version.py
}

build() {
  cd traitlets-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd traitlets-$pkgver
  pytest -v
}

package() {
  cd traitlets-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
