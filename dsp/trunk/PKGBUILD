# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Michael Barbour <barbour.michael.0@gmail.com>
# Contributor: sekret

pkgname=dsp
_commit='f3ac94b546361260349edeea5f1ee0ce7fac322a'
pkgver=v1.8.r5.gf3ac94b
pkgrel=1
pkgdesc="An audio processing program with an interactive mode"
arch=('x86_64')
url="https://github.com/bmc0/${pkgname}"
license=('ISC')
groups=('lasdpa-plugins')
makedepends=('git' 'ladspa')
depends=(
  'libmad' 'libtool'
  'ffmpeg' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  #'ffmpeg4.4'
  'alsa-lib' 'libasound.so'
  'fftw' 'libfftw3.so'
  'libao' 'libao.so'
  'libpulse' 'libpulse.so' 'libpulse-simple.so'
  'libsndfile' 'libsndfile.so'
  'zita-convolver' 'libzita-convolver.so'
)
optdepends=(
  'ladspa-host: For the LADSPA plugin.'
)
source=("git+https://github.com/bmc0/${pkgname}.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
