# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.16.4
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz")
sha512sums=('d54de9e6d5c80a0103a00ddd9a093b415403b24e0e197f9039a0599ae371e0cb840825ea3d24457ac8cb9d809c980331dc6fa5861e14016748cca97db1165772')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
