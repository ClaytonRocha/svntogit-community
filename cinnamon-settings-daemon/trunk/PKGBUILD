# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.4.0
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
         https://github.com/linuxmint/cinnamon-settings-daemon/commit/745e720e.patch)
sha512sums=('7f7f3d7d47a243bf9788ab39a57b744687751b4d168a2d797bc53a5f02cd96180ea3cf268187279d81bad54c46146ed0f6ee8f8255e6a52e42c656091dd015f4'
            'eaf0b4d391ec8994476b5e0f207695f07760eded10f132cdbb15aa93c682c1a0403c36c2009f2a7a8edb602b1f2baab4c1ae2912257955c04e4c4806c159770c')
b2sums=('528af77346221f58435befd2f8ce3f1713f0a2b4cf0181ca8256351d7eb7877ee1aebc3e3bf7ee2069a616484c1e8ded5cb158d7241adfaf0cf9f2205a93c199'
        '177e04fcf4593028c8b7bf2f884700df21349da3cebe6defcdf5d9636d53ac9fa37e4bfcdc1bd7bf876020c5a3cb17bb79432fbf2ca4599b9b2862b84d90d9be')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../745e720e.patch # Fix https://github.com/linuxmint/cinnamon/issues/10850
}

build() {
    mkdir -p ${pkgname}-${pkgver}/builddir
    cd ${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          ..

    samu
}

package() {
    cd ${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
