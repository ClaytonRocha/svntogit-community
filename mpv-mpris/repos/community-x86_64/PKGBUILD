# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=mpv-mpris
pkgver=0.8
pkgrel=1
pkgdesc='MPRIS plugin for mpv'
arch=('x86_64')
url='https://github.com/hoyon/mpv-mpris'
license=('MIT')
depends=('glib2' 'mpv')
checkdepends=('jq' 'playerctl' 'socat' 'sound-theme-freedesktop' 'xorg-server-xvfb')
options=('debug')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('97387caa98296bbcb71c847ada45f8baac8cfb9536554d38accd706800d9b867ac0012d330e9b548926184fff9e4952ff90b22a3e7d42c68ac74fbd6d61c3006')

build() {
	cd "$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
