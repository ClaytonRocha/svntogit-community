# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=selene
pkgname=selene-linter
pkgver=0.18.1
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('bfd66c9cbe77edbceb617d6acc83dad9a42bdc541be381b87883fedb0e185d4c4459862e9ac1ef8a3830e82596a9ad8c69114dfde858534a236a6bf6ad5ccd00')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
}

# vim:set ts=2 sw=2 et:
