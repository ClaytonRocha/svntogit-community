# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krita-plugin-gmic
pkgver=3.1.4.1
pkgrel=1
pkgdesc='GMic plugin for Krita'
arch=(x86_64)
url='https://github.com/amyspark/gmic'
license=(custom:CeCILL)
depends=(krita gmic)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://github.com/amyspark/gmic/releases/download/v$pkgver/gmic-$pkgver-patched.tar.xz{,.asc}
        fix-build.patch)
sha256sums=('e655b8affd3f9e0db7e9d4e25ba7c4d63cb958c84bca2da37fa58e310bbf7e9d'
            'SKIP'
            '3df2ae458f94f2d9aa96df933c7598f986e0f6ff1d088d7fcb5523cbb41cc77f')
validpgpkeys=(4894424D2412FEE5176732A3FC00108CFD9DBF1E) # https://github.com/amyspark.gpg
options=(debug)

prepare() {
  patch -d gmic-$pkgver-patched -p1 < fix-build.patch
}

build() {
  cmake -B build -S gmic-$pkgver-patched/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMIC_QT_HOST=krita-plugin
  VERBOSE=1 cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm "$pkgdir"/usr/share/gmic/gmic_cluts.gmz # Provided by gmic
  install -Dm644 gmic-$pkgver-patched/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
