# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=bash-language-server
pkgver=2.1.0
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/bash-lsp/$pkgname"
license=("MIT")
depends=("nodejs<18.1.0" "acorn")
makedepends=("yarn" "typescript" "git")
optdepends=('bash-completion: for CLI args completion')
source=("$url/archive/server-$pkgver.tar.gz")
sha512sums=('4a1814636345595b6561a8bb4a0a7cf48e52a9a86e6ea16120b3ef55d37502561be9fcaa4bc03e6eedbdaaee08d97398f17d02c6c4683c8810ef0aeb36e0dc15')

build() {
    cd "$pkgname-server-$pkgver"
    yarn
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcdir/$pkgname-server-$pkgver/LICENSE"

    rm -r node_modules/acorn
    mkdir -p node_modules/acorn/bin
    ln -s /usr/bin/acorn node_modules/acorn/bin/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
