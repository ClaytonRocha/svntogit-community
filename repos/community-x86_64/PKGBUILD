# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=readstat
pkgver=1.1.7
pkgrel=2
pkgdesc='Command-line tool (+ C library) for converting SAS, Stata, and SPSS files'
arch=(x86_64)
license=(MIT)
url='https://github.com/WizardMac/ReadStat'
depends=(zlib)
source=(https://github.com/WizardMac/ReadStat/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        readstat-LICENSE::https://raw.githubusercontent.com/WizardMac/ReadStat/v$pkgver/LICENSE)
sha256sums=('400b8e6a5f0f6458227b454785d68beadd8a88870a7745d49def49740e3971a8'
            '416b611ef9448b3c7c71f584b1ebf78baff8f553b4b5368f2af732255ca19a3e')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../readstat-LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
