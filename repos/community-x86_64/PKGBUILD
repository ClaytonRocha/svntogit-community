# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=webp-pixbuf-loader
pkgver=0.0.4
pkgrel=2
pkgdesc="WebM GDK Pixbuf Loader library"
arch=(x86_64)
url="https://github.com/aruiz/webp-pixbuf-loader"
license=(LGPL)
depends=(gdk-pixbuf2 libwebp)
makedepends=(meson)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-gdk_pixbuf_file_info-crash.patch::${url}/commit/631c1ec9dd3cf763c1431080940f830c6111b1d5.patch)
sha256sums=('cd6e4ec44755e8df3e298688c0aeb72b9467bbdd03009989c0d94b219b30fb51'
            '303fe5c039622fdfb2df8dbe650bc3cc1e2083decba915bbe449d627c5e6d2b4')

prepare() {
  patch -p1 -d ${pkgname}-${pkgver} < ${pkgname}-fix-gdk_pixbuf_file_info-crash.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
