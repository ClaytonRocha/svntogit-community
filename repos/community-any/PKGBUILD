# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_name=flake8-black
pkgname=python-flake8-black
pkgver=0.3.2
pkgrel=1
pkgdesc='Flake8 plugin to call black as a code style validator'
arch=('any')
url=https://github.com/peterjc/flake8-black
license=('MIT')
depends=('flake8' 'python-black' 'python-tomli')
makedepends=('python-setuptools')
source=("https://github.com/peterjc/$_name/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('7eaac0f263efc7e3dcb697fefbef17142b45f9333d33baa8cfe61786e66ecba378a59aab0283764d09b0688c546b31481d468aa41aae9dd39bc4b14e0d4513ad')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  source test-env/bin/activate
  python setup.py install --optimize=1 --skip-build
  cd tests
  ./run_tests.sh
  deactivate
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.rst
}

# vim:set ts=2 sw=2 et:
