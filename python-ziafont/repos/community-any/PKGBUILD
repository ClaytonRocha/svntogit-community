# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziafont
pkgver=0.3
pkgrel=1
pkgdesc="Convert TTF/OTF font glyphs to SVG paths"
url="https://ziafont.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nbval' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/ziafont/get/$pkgver.tar.bz2")
sha512sums=('ffc4c96f54b2a9084750800a383284c11a57fd0de0693c9ebbc93cdf2e2048e4b978683cf90e06f8eaffed90cf136d425c5a358c0277a843b952623b593b9b37')

prepare() {
  mv cdelker-ziafont-* ziafont-$pkgver
}

build() {
  cd ziafont-$pkgver
  python setup.py build
}

check() {
  cd ziafont-$pkgver
  python setup.py pytest --addopts --nbval || echo "Tests failed"
}

package() {
  cd ziafont-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
