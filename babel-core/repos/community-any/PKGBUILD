# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=babel-core
pkgver=7.17.10
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs' 'semver')
makedepends=('npm')
source=("$pkgname-$pkgver.tgz::https://registry.npmjs.org/@babel/core/-/core-$pkgver.tgz"
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('9622a8a696a7745dd97019c86058df4831d92ca74b1c62519285ad1bccd0c8624141f218a1ba67546239fa92c136d4baa6c14b0f3caaf3e8791e25658d6f4f34'
            'fc5471459770967e2632efb110b8370caa3424b7d87c1180f49605f7e6b1593746888b6c99c713c0c3046c5833c01c1a15bf3d549a343eaa077885bcc9f3d4e3')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -r "$pkgdir"/usr/lib/node_modules/@babel/core/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
