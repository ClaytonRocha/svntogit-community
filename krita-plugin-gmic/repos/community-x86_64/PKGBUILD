# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krita-plugin-gmic
pkgver=3.1.2.1
pkgrel=1
pkgdesc='GMic plugin for Krita'
arch=(x86_64)
url='https://github.com/amyspark/gmic'
license=(custom:CeCILL)
depends=(krita gmic)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://github.com/amyspark/gmic/releases/download/v$pkgver/gmic-$pkgver-patched.tar.xz{,.asc})
sha256sums=('eb6cec9de254d2323fab2f4eac6f7a65310728617110cd41a9067774138e241f'
            'SKIP')
validpgpkeys=(4894424D2412FEE5176732A3FC00108CFD9DBF1E) # https://github.com/amyspark.gpg
options=(debug)

prepare() {
  sed -e 's|-Wl,--stack,16777216|-Wl,-z,stack-size=16777216|' -i gmic-$pkgver-patched/gmic-qt/CMakeLists.txt
}

build() {
  cmake -B build -S gmic-$pkgver-patched/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMIC_QT_HOST=krita-plugin
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm "$pkgdir"/usr/share/gmic/gmic_cluts.gmz # Provided by gmic
  install -Dm644 gmic-$pkgver-patched/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
