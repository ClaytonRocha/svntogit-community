# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=4.2.0
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('c-ares' 'python-cffi')
makedepends=('python-setuptools')
checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('b286649597791cd53072b2f3383cc38fc14a8ab016b78cb04bdcaa6ecce3b8ce')
b2sums=('6b272c26e2e91706f208262d53d1ad909be47b14a2f5ac912ec8851f98e2d621698194ecc1962a8e921abd7ea09f08d9e766324ecca4628f3ad61b73ff48e2b5')

build() {
  cd pycares-$pkgver
  PYCARES_USE_SYSTEM_LIB=1 python setup.py build
}

# Test failures: test_custom_resolvconf, test_getaddrinfo{,2..5},
#                gethostbyaddr{,6}, test_getnameinfo
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --root=temp --optimize=1 --skip-build
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
