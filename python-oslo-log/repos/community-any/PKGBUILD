# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=4.8.0
pkgrel=1
pkgdesc="Oslo Logging Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("https://github.com/openstack/oslo.log/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('74ab95aed130e076178ab38fe1b5d5441df1bdfddfc7623cec7703bb02706853a5f0432faa54590dedf7a501de075dc821b26bbc82026b59d9c9926c26ac71f2')

export PBR_VERSION=$pkgver

build() {
  cd oslo.log-$pkgver
  python setup.py build
}

check() {
  cd oslo.log-$pkgver
  stestr run
}

package() {
  cd oslo.log-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
