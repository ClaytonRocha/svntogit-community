# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.10.1
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('imagemagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('daf72d66c9a569a214c8f51f01f2be4aef89a92f6bf25e777636bee0137087d0ab326f23b95fcb61938b381200b014aeeb58f8556130f2db500527db0d0763cd'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
