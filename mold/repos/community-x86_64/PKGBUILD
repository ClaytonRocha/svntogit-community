# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.3.0
pkgrel=1
pkgdesc='A Modern Linker'
arch=('x86_64')
url='https://github.com/rui314/mold'
license=('AGPL3')
# tbb & xxhash are bundled
# https://github.com/oneapi-src/oneTBB/pull/824
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib')
makedepends=('git' 'cmake')
checkdepends=('clang' 'libdwarf')
options=('debug')
_commit='a9a8205be29f36346bfdbfadee8ed147e06aa1f6'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use /usr/lib instead of /usr/libexec
  sed -i "s/libexec/lib/" Makefile
}

build() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1
}

check() {
  cd "$pkgname"

  make \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    check
}

package() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    DESTDIR="$pkgdir" \
    install

  ln -vsf /usr/bin/mold "$pkgdir/usr/lib/mold/ld"
}
